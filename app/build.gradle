apply plugin: 'com.android.application'

android {
    signingConfigs {
        config {
            storeFile file('/Users/hallmarklabs/.ssh/com.munger.debug-keystore.jks')
            storePassword '1600 Amphitheatre Parkway'
            keyAlias 'key0'
            keyPassword '1600 Amphitheatre Parkway'
        }
    }
    compileSdkVersion 25
    buildToolsVersion '25.0.3'
    defaultConfig {
        applicationId "com.munger.passwordkeeper"
        minSdkVersion 18
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }
        signingConfig signingConfigs.config
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            signingConfig signingConfigs.config
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    externalNativeBuild {
        cmake {
            path '../libpasswordkeeper/CMakeLists.txt'
        }
    }
}

afterEvaluate { project ->
    project.tasks.assembleDebugAndroidTest << {

        def rootDir = project.rootDir
        def localProperties = new File(rootDir, "local.properties")
        def sdkDir = '.'
        if (localProperties.exists()) {
            Properties properties = new Properties()
            localProperties.withInputStream { instr ->
                properties.load(instr)
            }
            sdkDir = properties.getProperty('sdk.dir')
        }

        exec {
            commandLine sdkDir + '/build-tools/25.0.3/apksigner',
                'sign', '--ks', android.signingConfigs.config.storeFile,
                '--ks-pass', 'pass:' + android.signingConfigs.config.storePassword,
                './build/outputs/apk/app-debug-androidTest.apk'

        }
    }
}

repositories {
    jcenter()
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'com.crittercism.dexmaker:dexmaker-mockito:1.4'
    androidTestCompile 'com.crittercism.dexmaker:dexmaker:1.4'
    androidTestCompile 'com.crittercism.dexmaker:dexmaker-dx:1.4'
    androidTestCompile 'com.android.support:support-annotations:25.3.1'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2.2', {
        exclude group: 'com.android.support', module: 'recyclerview-v7'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'support-v4'
    })
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:2.2.2'
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.android.support:preference-v7:25.3.1'
    compile 'com.google.android.gms:play-services-drive:11.0.1'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.powermock:powermock-api-mockito:1.6.6'
    testCompile 'org.powermock:powermock-module-junit4:1.6.6'
    compile project(':libpasswordkeeper')
}
